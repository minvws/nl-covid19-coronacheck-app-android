plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.navigation.safeargs.kotlin)
    alias(libs.plugins.kotlin.parcelize)
    alias(libs.plugins.shot)
    alias(libs.plugins.ksp)
    alias(libs.plugins.fladle)
}

def appVersionCode = 1000000
if (System.getenv("GITHUB_REPOSITORY_OWNER") == "minvws") {
    appVersionCode = System.getenv("GITHUB_RUN_NUMBER") != null ? System.getenv("GITHUB_RUN_NUMBER").toInteger() : 1000000
}
version = "4.9"
archivesBaseName = "holder-v${version}-${appVersionCode}"

android {
    namespace 'nl.rijksoverheid.ctr.holder'
    testOptions {
        unitTests {
            includeAndroidResources = true
            unitTests.all {
                jvmArgs '-Xmx2g'
            }
        }
    }

    sourceSets {
        debug {
            // for testing Room migrations, see https://github.com/robolectric/robolectric/issues/3928#issuecomment-395309991
            assets.srcDirs += files("$projectDir/schemas".toString())
        }
    }

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        applicationId "nl.rijksoverheid.ctr.holder"
        versionCode appVersionCode
        versionName version
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "com.karumi.shot.ShotTestRunner"

        buildConfigField "String", "SIGNATURE_CERTIFICATE_CN_MATCH", '".coronatester.nl"'
        buildConfigField "boolean", "FEATURE_CORONA_CHECK_API_CHECKS", "true"
        buildConfigField "boolean", "FEATURE_TEST_PROVIDER_API_CHECKS", "true"
        buildConfigField "String", "OPEN_ID_REDIRECT_URL", "\"https://coronacheck.nl/app/auth2\""
        buildConfigField "String", "BASE_API_URL", "\"https://holder-api.coronacheck.nl/v9/\""
        buildConfigField "String", "MAX_BASE_URL", "\"https://max.coronacheck.nl\""
        buildConfigField "String", "OPEN_ID_CLIENT_ID", "\"cc_app\""
        buildConfigField "String", "DEEPLINK_SCANNER_TEST_URL", "\"\""
        buildConfigField "String", "MIJNCN_BASEURL", "\"https://emax.coronacheck.nl\""
        buildConfigField "String", "PAP_BASE_URL", "\"https://qrcodezonderdigid.ggdghor.nl\""
        buildConfigField "String", "CDN_API_URL", "\"https://holder-api-cdn.coronacheck.nl/v9/\""
        manifestPlaceholders = [appLabel: "@string/app_name", deepLinkHost: "coronacheck.nl", digidSchema: "coronacheck"]
    }

    signingConfigs {
        release {
            storeFile file('../keystore.jks')
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEYSTORE_KEY_ALIAS")
            keyPassword System.getenv("KEYSTORE_KEY_PASSWORD")
        }
    }

    flavorDimensions "environment"
    productFlavors {
        tst {
            buildConfigField "String", "BASE_API_URL", "\"https://holder-api.test.coronacheck.nl/v9/\""
            buildConfigField "String", "CDN_API_URL", "\"https://holder-api.test.coronacheck.nl/v9/\""
            buildConfigField "String", "MAX_BASE_URL", "\"https://max.test.coronacheck.nl\""
            buildConfigField "String", "OPEN_ID_CLIENT_ID", "\"cc_app\""
            buildConfigField "String", "OPEN_ID_REDIRECT_URL", "\"https://web.test.coronacheck.nl/app/auth2\""
            buildConfigField "String", "DEEPLINK_SCANNER_TEST_URL", "\"https://web.acc.coronacheck.nl/verifier/scan?returnUri=https://web.test.coronacheck.nl/app/open?returnUri=scanner-test\""
            buildConfigField "String", "MIJNCN_BASEURL", "\"https://emax.test.coronacheck.nl\""
            buildConfigField "String", "PAP_BASE_URL", "\"https://pap.bananenhalen.nl/\""
            dimension "environment"
            versionNameSuffix "-test"
            applicationIdSuffix ".test"
            manifestPlaceholders = [appLabel: "@string/app_name_test", deepLinkHost: "web.test.coronacheck.nl", digidSchema: "coronacheck-test"]
            apply plugin: 'com.google.gms.google-services'
            apply plugin: 'com.google.firebase.crashlytics'
            matchingFallbacks = ['mlkit']
            buildConfigField "boolean", "FEATURE_TEST_PROVIDER_API_CHECKS", "true"
            buildConfigField "boolean", "FEATURE_CORONA_CHECK_API_CHECKS", "false"
        }
        acc {
            buildConfigField "String", "BASE_API_URL", "\"https://holder-api.acc.coronacheck.nl/v9/\""
            buildConfigField "String", "CDN_API_URL", "\"https://holder-api-cdn.acc.coronacheck.nl/v9/\""
            buildConfigField "String", "MAX_BASE_URL", "\"https://max.acc.coronacheck.nl\""
            buildConfigField "String", "OPEN_ID_CLIENT_ID", "\"cc_app\""
            buildConfigField "String", "OPEN_ID_REDIRECT_URL", "\"https://web.acc.coronacheck.nl/app/auth2\""
            buildConfigField "String", "DEEPLINK_SCANNER_TEST_URL", "\"https://web.acc.coronacheck.nl/verifier/scan?returnUri=https://web.acc.coronacheck.nl/app/open?returnUri=scanner-test\""
            buildConfigField "String", "MIJNCN_BASEURL", "\"https://emax.acc.coronacheck.nl\""
            buildConfigField "String", "PAP_BASE_URL", "\"https://acceptatie.qrcodezonderdigid.ggdghor.nl\""
            dimension "environment"
            versionNameSuffix "-acc"
            applicationIdSuffix ".acc"
            manifestPlaceholders = [appLabel: "@string/app_name_acc", deepLinkHost: "web.acc.coronacheck.nl", digidSchema: "coronacheck-acc"]
            apply plugin: 'com.google.gms.google-services'
            apply plugin: 'com.google.firebase.crashlytics'
            matchingFallbacks = ['mlkit']
            buildConfigField "boolean", "FEATURE_TEST_PROVIDER_API_CHECKS", "true"
            buildConfigField "boolean", "FEATURE_CORONA_CHECK_API_CHECKS", "true"
        }
        fdroidAcc {
            buildConfigField "String", "BASE_API_URL", "\"https://holder-api.acc.coronacheck.nl/v8/\""
            buildConfigField "String", "CDN_API_URL", "\"https://holder-api-cdn.acc.coronacheck.nl/v8/\""
            buildConfigField "String", "MAX_BASE_URL", "\"https://max.acc.coronacheck.nl\""
            buildConfigField "String", "OPEN_ID_CLIENT_ID", "\"cc_app\""
            buildConfigField "String", "OPEN_ID_REDIRECT_URL", "\"coronacheck-acc://auth/login\""
            buildConfigField "String", "PAP_BASE_URL", "\"https://acceptatie.qrcodezonderdigid.ggdghor.nl\""
            dimension "environment"
            versionNameSuffix "-fdroid"
            applicationIdSuffix ".fdroid.acc"
            manifestPlaceholders = [appLabel: "@string/app_name_acc", deepLinkHost: "web.acc.coronacheck.nl", digidSchema: "coronacheck-acc"]
            apply plugin: 'com.google.gms.google-services'
            apply plugin: 'com.google.firebase.crashlytics'
            matchingFallbacks = ['zxing']
        }
        prod {
            dimension "environment"
            matchingFallbacks = ['mlkit']
        }
        fdroidProd {
            buildConfigField "String", "OPEN_ID_REDIRECT_URL", "\"coronacheck://auth/login\""
            dimension "environment"
            matchingFallbacks = ['zxing']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            productFlavors.tst.signingConfig signingConfigs.release
            productFlavors.acc.signingConfig signingConfigs.release
            productFlavors.prod.signingConfig signingConfigs.release
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
    }

    ksp {
        arg("room.schemaLocation", "$projectDir/schemas")
    }

    kotlin {
        sourceSets {
            main.kotlin.srcDirs += 'build/generated/ksp/main/kotlin'
            test.kotlin.srcDirs += 'build/generated/ksp/test/kotlin'
        }
    }

    packagingOptions {
        exclude "META-INF/LICENSE**"
        pickFirst "MANIFEST.MF"
    }

    def end2endPassword = project.ext.has("android.testInstrumentationRunnerArguments.authPassword") ? project.ext.getProperty("android.testInstrumentationRunnerArguments.authPassword") : ""
    fladle {
        projectId = "coronatester-test"
        variant = "accDebug"
        maxTestShards = 1
        testTargets = ["notPackage nl.rijksoverheid.ctr.holder.end2end"]
        devices = [
                ["model": "Pixel2.arm", "version": "29"]
        ]
        configs {
            end2end {
                resultsHistoryName.set("end2end")
                testTargets.set(["package nl.rijksoverheid.ctr.holder.end2end"])
                environmentVariables.set(["authPassword": end2endPassword])
                devices.set([
                        ["model": "Pixel2.arm", "version": "29", "locale": "nl"]
                ])
            }
        }
        flankVersion = "22.05.0"
    }
}

dependencies {
    implementation project(":shared")
    implementation project(":appconfig")
    implementation project(":design")
    implementation project(":introduction")
    implementation project(":api")
    implementation project(':mobilecore')
    implementation project(":qrscanner")
    implementation project(":modules:luhncheck")
    implementation project(":modules:qrgenerator")
    implementation project(":modules:openidconnect")

    implementation libs.openid.appauth
    implementation libs.bundles.groupie
    implementation libs.navigation.fragment
    implementation libs.navigation.ui
    implementation libs.koin.android
    implementation libs.androidx.preference
    implementation libs.constraintlayout
    implementation libs.timber
    implementation libs.lottie
    debugImplementation libs.fragment.testing
    implementation libs.zxing.core
    implementation(platform(libs.okhttp.bom))
    implementation libs.okhttp.logging.interceptor
    implementation libs.okhttp
    implementation libs.okhttp.tls
    implementation libs.retrofit.moshi
    implementation libs.androidx.security.crypto
    implementation libs.rootbeer
    implementation libs.room.runtime
    implementation libs.room
    ksp libs.room.compiler
    implementation libs.sqlcipher
    implementation libs.work.runtime
    implementation libs.moshi.adapters

    testImplementation libs.junit
    testImplementation libs.mockk
    androidTestImplementation libs.mockk.android
    testImplementation libs.koin.test.junit4
    testImplementation(libs.robolectric) {
        exclude group: 'org.bouncycastle'
    }
    testImplementation libs.androidx.test.runner
    testImplementation libs.okhttp.mockWebServer
    testImplementation(libs.barista) {
        exclude group: "org.jetbrains.kotlin"
    }
    testImplementation libs.navigation.testing
    testImplementation libs.coroutines.android
    testImplementation libs.room.testing
    testImplementation libs.work.testing
    testImplementation libs.coroutines.test
    testImplementation libs.androidx.core.testing
    testImplementation libs.espresso.core

    coreLibraryDesugaring libs.desugar

    accImplementation libs.crashlytics

    androidTestImplementation libs.androidx.test.junit
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.test.rules
    androidTestImplementation libs.androidx.test.core
    androidTestImplementation libs.androidx.core.testing
    androidTestImplementation libs.uiautomator
    androidTestImplementation(libs.barista) {
        exclude group: "org.jetbrains.kotlin"
    }

    ksp libs.moshi.codegen
}
